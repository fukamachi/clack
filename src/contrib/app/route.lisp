#|
  This file is a part of Clack package.
  URL: http://github.com/fukamachi/clack
  Copyright (c) 2011 Eitarow Fukamachi <e.arrows@gmail.com>
  Copyright (c) 2011 Tomohiro Matsuyama <tomo@cx4a.org>

  Clack is freely distributable under the LLGPL License.
|#

(clack.util:namespace clack.app.route
  (:use :cl
        :clack)
  (:import-from :cl-ppcre
                :scan-to-strings
                :split
                :quote-meta-chars)
  (:import-from :alexandria
                :with-gensyms))

(cl-annot:enable-annot-syntax)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun compile-path (path)
    (loop with list = (split ":([\\w-]+)" path :with-registers-p t)
          while list
          for prefix = (pop list)
          for name = (pop list)
          collect (quote-meta-chars prefix) into parts
          if name
            collect (string-upcase name) into names
            and collect "(.+?)" into parts
          finally
       (return (list (format nil "^窿あ疳螋螬钺礤螬┅┅厘痫螋ㄤ彐磲泸溴骝秕翦钺礤怙澌蝻豸弩狨雉桢蝼轶灬篝蝻豸弩┅ㄩ礤礅弪ㄣ狎雉桢蝼轶濠Ж雉桢蝼轶濠箦翩蝻豸弩ㄢ豸灬篝蝻豸弩┅箦翩雉桢蝼轶铋飑鏖翳珏铙眢蝈蝈聃弩舡礤翳镤蝈聃弩舡疳翳磲翥桢蝈珞啜溴骢钺礤ì蝈瘵戾è蝈聃弩舡礤翳镤ㄧ弭蝈候羼蹂篝礤翳镤┅ì蝈聃弩舡疳翳ㄧ弭蝈吼狒璀轭骘┅ㄤ邈灬蝈ㄩ珙矧徕戾蝈聃弩舡礤翳镤蝈聃弩舡疳翳┅矧括祜镳骘礤翳镤疳翳骘蝽轭蝻豸弩骘蝈珏钺礤螬ㄣ镯痖戾疳翳疳翳骘簌礅镬磲疸狎灬礅溽钺礤ㄩ铘弪钺礤疳汶徵濯┅钺礤螬泔祆邈啜犷篝蜷铉蝈聃弩舡礤翳镤К礤翳镤眭祠轲戾鲠祯瀛忾钿ì磲翥桢蝈珞筱犷麸篝蜷铉蝈珏蝈聃弩舡疳翳ㄤ邈灬蝈ㄩ珙矧徕戾蝈珞┅ㄩ磲翥桢ㄩ簌礅镬啜溴篝蝓泗躜轭绛忾钿簌礅镬ㄣ镥蜚蝈珞ъ轶舂ㄤ邈灬蝈ㄩ珙矧徕戾荔礅镬螬ㄣ犰骘蝽蝈瘵啜汜祆骘蝽蝈瘵┅┅ㄩ雉桢蝼轶啜汜祆ㄣ徜雉桢蝼轶濠蝈瘵Ж扉篝窗铋铋飑┅┅┅ㄤ镢后翎螋冷镢何镣渺徙氘琉甬绎豸找溟箴狒汨弪冷镢河傥闲由ㄤ彐疳汶徵沆徙氕筢眇戾ê躞恒恒灬汶恒灬汶狃甬蝻豸濠ㄩ瞽疳汶徵恒灬汶筢眇戾ㄤ彐蝻豸狃蝈瘵ㄇ旁堍堍＇轭溴ㄇ旁堍祜玳钴＇祜玳瞟ㄐ嫌堍祜玳钴＇狨翳矧辁濠ㄇ旁堍礤礅弪洪滠＇礤礅弪┅ㄣ灬汶躔＇狃皓冷镢耗庞靡尚陨衔渺徙氘琉甬绎豸痱秭殇弩犷找忉箦溟箴徙桢颥轭箴轵邃怡阴怡娱钺趄岙冷镢毫赵认燥盹栝蝻歪趔貔犴麸盹楞瘁矧绌